import QtQuick 1.1
import "Sort.js" 1.0 as Sort

Column{
    id: raceManager
    property string sortorder:"number"
    property int racetime:0
    //anchors.horizontalCenter: parent.horizontalCenter
    width:320
    spacing:25
    


    GridDecor {
        id: raceRect
        name:"Race"
        width:294
        height:294
        //anchors.horizontalCenter: parent.horizontalCenter;
        GridView {
            id: raceGrid
            boundsBehavior: Flickable.StopAtBounds
            anchors.topMargin: 5
            anchors.leftMargin: 5
            anchors.fill: parent

            cellWidth: 72
            cellHeight: 72
            snapMode: GridView.SnapToRow
            model: raceList;
            delegate: Item {
                VKart {id:rkart;team:model.team; num:kart; lifetime: model.lifetime; hottime: model.hottime; quality:model.quality; broken:model.broken}
                MouseArea {
                    anchors.fill:rkart
                    onClicked:racePopupMenu.openPopupMenuFor(rkart,"RacePopupMenu",rkart.num,raceManager)
                }
            }
        }
    }

    GridDecor {
        id: pitRect
        name:"Pit"
        width: 294
        height: 80
        GridView {
            id:pitGrid
            anchors.topMargin: 5
            anchors.leftMargin: 5
            anchors.fill: parent
            snapMode: GridView.SnapOneRow
            boundsBehavior: Flickable.StopAtBounds
            flickableDirection: Flickable.AutoFlickDirection
            clip:true
            flow:GridView.TopToBottom

            cellWidth:66
            cellHeight: 72
            model: pitList
            delegate: Item {
                VKart {id:pkart;num:kart; quality:model.quality; broken:model.broken}
                MouseArea {
                    anchors.fill:pkart
                    onClicked:pitPopupMenu.openPopupMenuFor(pkart,"PitPopupMenu",pkart.num,raceManager)
                }
            }
        }
    }

    GridDecor {
        id: spareRect
        name:"Spare"
        width: 294
        height: 80
        anchors.bottom:raceManager.bottom

        GridView {
            id:spareGrid
            anchors.topMargin: 5
            anchors.leftMargin: 5
            anchors.fill: parent
            snapMode: GridView.SnapOneRow
            boundsBehavior: Flickable.StopAtBounds
            flickableDirection: Flickable.AutoFlickDirection
            clip:true
            flow:GridView.TopToBottom

            cellWidth:66
            cellHeight: 72
            model: spareList
            delegate: Item {
                VKart {id:skart;team:model.team; num:kart; lifetime: model.lifetime; hottime: model.hottime; quality:model.quality; broken:model.broken}
                MouseArea {
                    anchors.fill:skart
                    onClicked:sparePopupMenu.openPopupMenuFor(skart,"SparePopupMenu",skart.num,raceManager)
                }
            }
        }
    }


    Image {
        id: swapBtn
        width: 60
        height: 60
        anchors.verticalCenter: pitRect.verticalCenter
        anchors.left:pitRect.right
        anchors.leftMargin: 2
        fillMode: Image.PreserveAspectCrop
        source: "img/swap.png"
        MouseArea {
            anchors.fill:parent
            acceptedButtons: Qt.LeftButton
            onClicked: {
                shiftPopupMenu.openPopupMenuFor(swapBtn,"ShiftPopupMenu",raceList,raceManager)
            }
        }
    }

    SideMenu {
        id:sideMenu
        anchors.top: raceRect.top
        anchors.left:raceRect.right
        anchors.leftMargin: 2
        onMenuSelected: raceManager.sortorder=eventid
    }

    signal menuSelected(string menuName,string menuItem,variant tag)
    signal raceTimeTick()
    signal raceTimeReset()
    signal reset()

    onRaceTimeTick: {
        racetime++
        for(var i=0;i<raceList.count;i++) raceList.setProperty(i,'lifetime',raceList.get(i).lifetime+1)

    }

    onRaceTimeReset: {
        racetime=0
        for(var i=0;i<raceList.count;i++) raceList.setProperty(i,'lifetime',0)
    }

    onReset: {
        raceList.clear()
        pitList.clear()
        spareList.clear()
        var lastnum
        lastnum=fillListWithKarts(raceList,main.kartsInRace,1)
        lastnum=fillListWithKarts(pitList,main.kartsInPit,lastnum)
        lastnum=fillListWithKarts(spareList,main.kartsInSpare,lastnum)
        fillListWithTeams(raceList,1)
        sortorder="kart"
        sortRaceList()
    }

    Component.onCompleted: reset()

    onMenuSelected: {
        switch (menuName) {
        case "RacePopupMenu": {
            var i=getIndexByProp(raceList,'kart',tag)
            var team=raceList.get(i).team
            switch (menuItem) {
            case "QUALITY_SLOW": {raceList.setProperty(i,'quality',-1);break}
            case "QUALITY_NEUTRAL": {raceList.setProperty(i,'quality',0);break}
            case "QUALITY_FAST": {raceList.setProperty(i,'quality',1);break}
            case "QUALITY_TRASH": {raceList.setProperty(i,'quality',-2);break}
            case "QUALITY_UNKNOWN":{ raceList.setProperty(i,'quality',-5);break}
            case "QUALITY_ROCKET": {raceList.setProperty(i,'quality',2);break}
            case "KART_BREAKAGE": {raceDoBreakage(raceList,pitList,spareList,team);break}
            case "KART_SHIFT":{raceDoShift(raceList,pitList,spareList,team);break}
            case "TEAM_DISQUAL":{raceDoDisqual(raceList,pitList,spareList,team);break}
            }
            sortRaceList()
            break
        }
        case "PitPopupMenu": {
            var i=getIndexByProp(pitList,'kart',tag)

            switch (menuItem) {
            case "QUALITY_SLOW": {pitList.setProperty(i,'quality',-1);break}
            case "QUALITY_NEUTRAL": {pitList.setProperty(i,'quality',0);break}
            case "QUALITY_FAST": {pitList.setProperty(i,'quality',1);break}
            case "QUALITY_TRASH": {pitList.setProperty(i,'quality',-2);break}
            case "QUALITY_UNKNOWN": {pitList.setProperty(i,'quality',-5);break}
            case "QUALITY_ROCKET": {pitList.setProperty(i,'quality',2);break}
            case "KART_MOVE": {pitDoMove(raceList,pitList,spareList,tag);break}
            case "KART_TOSPARE": {pitDoToSpare(raceList,pitList,spareList,tag);break}
            case "KART_BROKEN": {pitList.setProperty(i,'broken',true);break}
            }
            break
        }
        case "SparePopupMenu": {
            var i=getIndexByProp(spareList,'kart',tag)

            switch (menuItem) {
            case "QUALITY_SLOW": {spareList.setProperty(i,'quality',-1);break}
            case "QUALITY_NEUTRAL": {spareList.setProperty(i,'quality',0);break}
            case "QUALITY_FAST": {spareList.setProperty(i,'quality',1);break}
            case "QUALITY_TRASH": {spareList.setProperty(i,'quality',-2);break}
            case "QUALITY_UNKNOWN": {spareList.setProperty(i,'quality',-5);break}
            case "QUALITY_ROCKET": {spareList.setProperty(i,'quality',2);break}
            case "KART_TOPIT": {spareDoToPit(raceList,pitList,spareList,tag);break}
            case "KART_BROKEN": {spareList.setProperty(i,'broken',true);break}
            case "KART_REPAIRED": {spareList.setProperty(i,'broken',false);break}
            }
            break
        }

        case "ShiftPopupMenu": {
            raceDoShift(raceList,pitList,spareList,menuItem)
            sortRaceList()
            break
        }
        }
    }

    function getIndexByProp(list,prop,val) {
        for (var i=0;i<list.count;i++) {
            if (Sort.qs_prop(list,i,prop)==val) {
                return i
            }
        }
        return null
    }


    function raceDoShift(raceList,pitList,spareList,team) {
        var index=getIndexByProp(raceList,'team',team)
        var old_kart=raceList.get(index).kart
        var old_quality=raceList.get(index).quality
        var new_kart=pitList.get(0).kart
        var new_quality=pitList.get(0).quality

        raceList.remove(index)
        raceList.insert(0,{'kart':new_kart, 'team':team,'quality':new_quality,'lifetime':0,'hottime':main.timeToShift*60,'broken':false})
        pitList.remove(0)
        pitList.append({'kart':old_kart,'quality':old_quality,'team':'','lifetime':0,'broken':false})

    }

    function raceDoDisqual(raceList,pitList,spareList,team) {
        var index=getIndexByProp(raceList,'team',team)
        var old_kart=raceList.get(index).kart
        var old_quality=raceList.get(index).quality
        raceList.remove(index)
        pitList.append({'kart':old_kart,'quality':old_quality,'team':'','lifetime':0})
    }

    function raceDoBreakage(raceList,pitList,spareList,team) {
        var index=getIndexByProp(raceList,'team',team)
        var old_kart=raceList.get(index).kart
        var old_quality=raceList.get(index).quality
        var new_kart=pitList.get(0).kart
        var new_quality=pitList.get(0).quality

        raceList.remove(index)
        raceList.insert(0,{'kart':new_kart, 'team':team,'quality':new_quality,'lifetime':0,'hottime':main.timeToShift*60,'broken':false})
        pitList.remove(0)
        pitList.append({'kart':old_kart,'quality':old_quality,'team':'','lifetime':0, 'broken':true})

    }

    function pitDoMove(raceList,pitList,spareList,kart) {
        var index=getIndexByProp(pitList,'kart',kart)
        if (index!=0) {
            Sort.qs_swap(pitList,index-1,index,'kart')
        }
    }

    function pitDoToSpare(raceList,pitList,spareList,kart) {
        var index=getIndexByProp(pitList,'kart',kart)
        var old_kart=pitList.get(index).kart
        var old_quality=pitList.get(index).quality
        var old_broken=pitList.get(index).broken
        spareList.append({'kart':old_kart,'quality':old_quality,'broken':old_broken})
        pitList.remove(index)


    }

    function spareDoToPit(raceList,pitList,spareList,kart) {
        var index=getIndexByProp(spareList,'kart',kart)
        var old_kart=spareList.get(index).kart
        var old_quality=spareList.get(index).quality
        var old_broken=spareList.get(index).broken
        pitList.append({'kart':old_kart,'quality':old_quality,'broken':old_broken})
        spareList.remove(index)


    }


    function fillListWithKarts(list,size,startfrom) {
        var kart=startfrom
        for (var i=1;i<=size;i++) {
            list.append({'kart':kart++,'team':'','lifetime':0,'quality':-5,'hottime':main.timeToShift*60, 'broken':false})
        }
        return kart
    }

    function fillListWithTeams(list,startfrom) {
        var team=startfrom
        for (var i=0;i<list.count;i++) {
            list.setProperty(i,'team',team++)
        }
        return team
    }

    function sortRaceList() {
        Sort.qsort(raceList,sortorder)
    }

    onSortorderChanged: sortRaceList()

}
